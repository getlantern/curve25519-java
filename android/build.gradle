buildscript {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

repositories {
    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    api project(':java')

//    testCompile (project(":tests")) {
//        exclude module: 'junit'
//    }
}

signing {
   required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
   sign configurations.archives
}

// the below is needed in order to get the archive to publish with maven/Jitpack
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId    = group_info
                artifactId = "curve25519-android"
                version    = version_number
            }
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug
                groupId    = group_info
                artifactId = "curve25519-android-debug"
                version    = version_number
            }
        }
    }
}

task install(dependsOn: publishToMavenLocal) {}